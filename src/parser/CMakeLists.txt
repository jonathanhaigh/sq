find_path(ANTLR4_CPP_RUNTIME_INCLUDE_DIR
    NAMES "antlr4-runtime.h"
    PATH_SUFFIXES "antlr4-runtime"
    DOC "Path to the ANTLR4 C++ runtime include directory"
)

if (NOT ANTLR4_CPP_RUNTIME_INCLUDE_DIR)
    message(FATAL_ERROR "Failed to find ANTLR4 C++ runtime include directory")
endif()

set(SQ_ANTLR4_GEN_INCLUDE_DIR "${CMAKE_CURRENT_BINARY_DIR}/include")
set(SQ_ANTLR4_GEN_DIR "${SQ_ANTLR4_GEN_INCLUDE_DIR}/parser")

set(SQ_ANTLR4_GEN_HEADERS
    "${SQ_ANTLR4_GEN_DIR}/SqBaseVisitor.h"
    "${SQ_ANTLR4_GEN_DIR}/SqLexer.h"
    "${SQ_ANTLR4_GEN_DIR}/SqVisitor.h"
    "${SQ_ANTLR4_GEN_DIR}/SqParser.h"
)

set (SQ_ANTLR4_GEN_SRC
    "${SQ_ANTLR4_GEN_DIR}/SqBaseVisitor.cpp"
    "${SQ_ANTLR4_GEN_DIR}/SqLexer.cpp"
    "${SQ_ANTLR4_GEN_DIR}/SqVisitor.cpp"
    "${SQ_ANTLR4_GEN_DIR}/SqParser.cpp"
)

add_custom_command(
    OUTPUT ${SQ_ANTLR4_GEN_HEADERS} ${SQ_ANTLR4_GEN_SRC}
    COMMAND "${CMAKE_COMMAND}" -E make_directory "${SQ_ANTLR4_GEN_DIR}"
    COMMAND "antlr4"
        -o "${SQ_ANTLR4_GEN_DIR}"
        -Dlanguage=Cpp
        -Werror
        -no-listener
        -visitor
        -package "sq::parser"
        "${CMAKE_CURRENT_SOURCE_DIR}/Sq.g4"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/Sq.g4"
    VERBATIM
)

add_library(sq_parser
    ${SQ_ANTLR4_GEN_HEADERS}
    ${SQ_ANTLR4_GEN_SRC}
)

# This code is all generated by 3rd party libraries so don't do static analysis
set_target_properties(sq_parser PROPERTIES CXX_CLANG_TIDY "")

# ..and ignore a bunch of warnings
if ("${CMAKE_CXX_COMPILER_ID}" IN_LIST SQ_GCC_STYLE_COMPILERS)
    target_compile_options(sq_parser PRIVATE
        -Wno-attributes
        -Wno-overloaded-virtual
     )
endif()

# Antlr4 doesn't seem to compile as c++20 yet
set_property(TARGET sq_parser PROPERTY CXX_STANDARD 17)

# Use "SYSTEM" for the generated headers so that clang-tidy and the compiler
# don't complain about the style used in the generated headers when they are
# included from cpp files in other targets for which we do want to run static
# analysis.
target_include_directories(sq_parser SYSTEM PUBLIC ${SQ_ANTLR4_GEN_INCLUDE_DIR})

target_include_directories(sq_parser PUBLIC ${ANTLR4_CPP_RUNTIME_INCLUDE_DIR})

target_link_libraries(sq_parser PUBLIC antlr4-runtime)
